import org.gradle.internal.os.OperatingSystem

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
    id 'application'
}

group 'com.GwgCommunity'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

project.ext.lwjglVersion = "3.3.0"
project.ext.jomlVersion = "1.10.1"
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'scenes.TextRenderingDemo'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'scenes.DemoTopDown',
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/*.LIST"
    }
    with jar
}

sourceSets {
    test {
        java {
            srcDir 'src/main/test'
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"
}

task generateApiDocs(type: Javadoc) {
    title = ""
    options.noTimestamp(false)
    source sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.encoding 'UTF-8'
    destinationDir = file("build/generated-files")
    options.addStringOption("doclet", "com.microsoft.doclet.DocFxDoclet")
    options.docletpath = [file("lib/docfx-doclet-1.0-SNAPSHOT-jar-with-dependencies.jar")]
    dependsOn build
}

// Possible thing to look into later... this doesn't work, I am too tired rn to try making it work.
//task execute(type: JavaExec) {
//    classpath(sourceSets.main.runtimeClasspath)
//    main('com.name.enginetester.GameLoop')
//
//    if (os.isMacOsX()) {
//        jvmArgs '-XstartOnFirstThread'
//    }
//}

